variables.tf
variable "subscription_id" {
  description = "Azure Subscription ID"
  type        = string
  default     = "3cb5b17b-f702-4534-bf9d-f5e83dd19e4c"
}

variable "location" {
  description = "Azure Region"
  type        = string
  default     = "France Central"
}

variable "resource_group_name" {
  description = "Resource Group Name"
  type        = string
  default     = "learning"
}

variable "virtual_network_name" {
  description = "Virtual Network Name"
  type        = string
  default     = "vnet1"
}

variable "vnet_address_space" {
  description = "Virtual Network address space"
  type        = list(string)
  default     = ["10.0.0.0/16"]
}

variable "subnet_name" {
  description = "Subnet Name"
  type        = string
  default     = "vnet1-subnet"
}

variable "subnet_prefix" {
  description = "Subnet address prefix"
  type        = list(string)
  default     = ["10.0.1.0/24"]
}

variable "nsg_name" {
  description = "Network Security Group Name"
  type        = string
  default     = "my-nsg"
}

variable "nic_name" {
  description = "NIC Name"
  type        = string
  default     = "my-nic"
}

variable "public_ip_name" {
  description = "Public IP Name"
  type        = string
  default     = "my_public_ip"
}

variable "vm_name" {
  description = "VM Name"
  type        = string
  default     = "my-ubuntu-vm"
}

variable "vm_size" {
  description = "VM Size"
  type        = string
  default     = "Standard_B1s"
}

variable "admin_username" {
  description = "Admin Username"
  type        = string
  default     = "learning"
}

variable "disk_size" {
  description = "OS Disk Size (GB)"
  type        = number
  default     = 30
}

variable "public_ssh_key" {
  description = "SSH public key"
  type        = string
}


main.tf

provider "azurerm" {
  features {}
  use_cli         = true
  subscription_id = var.subscription_id
}

resource "azurerm_resource_group" "dev-resource-group" {
  name     = var.resource_group_name
  location = var.location
}

resource "azurerm_virtual_network" "dev-virtual-network" {
  name                = var.virtual_network_name
  address_space       = var.vnet_address_space
  location            = azurerm_resource_group.dev-resource-group.location
  resource_group_name = azurerm_resource_group.dev-resource-group.name
}

resource "azurerm_subnet" "dev-subnet" {
  name                 = var.subnet_name
  resource_group_name  = azurerm_resource_group.dev-resource-group.name
  virtual_network_name = azurerm_virtual_network.dev-virtual-network.name
  address_prefixes     = var.subnet_prefix
}

resource "azurerm_network_security_group" "dev_ntw_security_group" {
  name                = var.nsg_name
  location            = azurerm_resource_group.dev-resource-group.location
  resource_group_name = azurerm_resource_group.dev-resource-group.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  security_rule {
    name                       = "HTTP"
    priority                   = 1002
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "80"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }

  security_rule {
    name                       = "HTTPS"
    priority                   = 1003
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "443"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

resource "azurerm_subnet_network_security_group_association" "dev-ntw-sec-group_assoc" {
  subnet_id                 = azurerm_subnet.dev-subnet.id
  network_security_group_id = azurerm_network_security_group.dev_ntw_security_group.id
}

resource "azurerm_network_interface" "dev-nic" {
  name                = var.nic_name
  location            = azurerm_resource_group.dev-resource-group.location
  resource_group_name = azurerm_resource_group.dev-resource-group.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.dev-subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.dev_public_ip.id
  }
}

resource "azurerm_public_ip" "dev_public_ip" {
  name                = var.public_ip_name
  location            = azurerm_resource_group.dev-resource-group.location
  resource_group_name = azurerm_resource_group.dev-resource-group.name
  allocation_method   = "Static"
}

resource "azurerm_linux_virtual_machine" "dev-ubuntu-vm" {
  name                            = var.vm_name
  resource_group_name             = azurerm_resource_group.dev-resource-group.name
  location                        = azurerm_resource_group.dev-resource-group.location
  size                            = var.vm_size
  admin_username                  = var.admin_username
  network_interface_ids           = [azurerm_network_interface.dev-nic.id]
  disable_password_authentication = true

  admin_ssh_key {
    username   = var.admin_username
    public_key = var.public_ssh_key
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
    disk_size_gb         = var.disk_size
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts"
    version   = "latest"
  }

  custom_data = base64encode(<<EOF
#!/bin/bash
apt update -y
apt install apache2 -y
service apache2 restart
echo "Hello world" > /var/www/html/index.html
EOF
  )
}

output "public_ip" {
  value = azurerm_public_ip.dev_public_ip.ip_address
}


deploy.SSH
#!/bin/bash
set -e

KEY_FILE="my-ssh-key"
PEM_KEY_FILE="my-ssh-key.pem"
TF_VAR_ADMIN_USERNAME="learning"   # Update if your admin_username is different

# 1. Generate OpenSSH private key if it doesn't exist
if [ ! -f "$KEY_FILE" ]; then
  echo "Generating OpenSSH key pair..."
  ssh-keygen -t rsa -b 4096 -f $KEY_FILE -N ""
else
  echo "OpenSSH key already exists, reusing: $KEY_FILE"
fi

# 2. Export the public key for Terraform
export TF_VAR_public_ssh_key="$(cat ${KEY_FILE}.pub)"
export TF_VAR_admin_username="$TF_VAR_ADMIN_USERNAME"

# 3. Convert to classic PEM format if needed
if [ ! -f "$PEM_KEY_FILE" ]; then
  echo "Converting private key to PEM format for compatibility..."
  ssh-keygen -p -m PEM -f $KEY_FILE -N "" -P ""
  cp $KEY_FILE $PEM_KEY_FILE
else
  echo "PEM key already exists, reusing: $PEM_KEY_FILE"
fi

# 4. Run Terraform
terraform init
terraform apply -auto-approve

VM_IP=$(terraform output -raw public_ip)
echo ""
echo "=========================================================="
echo "VM Provisioned!"
echo "SSH into your VM using:"
echo ""
echo "Linux/macOS/WSL/Native SSH:"
echo "ssh -i $KEY_FILE $TF_VAR_ADMIN_USERNAME@$VM_IP"
echo ""
echo "MobaXterm/WinSCP/Putty or tools requiring PEM format:"
echo "ssh -i $PEM_KEY_FILE $TF_VAR_ADMIN_USERNAME@$VM_IP"
echo ""
echo "You can import either key into your SSH client as needed."
echo "=========================================================="
